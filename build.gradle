buildscript {
    ext.gradle_version = '4.1.3'
    ext.kotlin_version = '1.4.31'
    ext.kotlinx_version = '1.3.9'
    ext.kotlinx_serialization_version = '1.1.0'
    ext.material_library_version = '1.3.0'
    ext.constraintlayout_version = '2.0.4'
    ext.retrofit_version = '2.9.0'
    ext.koin_version = '2.2.2'
    ext.room_version = '2.2.6'
    ext.fragmentktx_version = '1.3.0'
    ext.cicerone_version = '6.6'
    ext.picasso_version = '2.71828'
    ext.appcompat_version = '1.2.0'
    ext.mp_android_chart_version = 'v3.1.0'
    ext.flexbox_version = '2.0.1'
    ext.objectbox_version = '2.9.1'

    repositories {
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.gms:google-services:4.3.5'
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "io.objectbox:objectbox-gradle-plugin:$objectbox_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenLocal() {
            metadataSources {
                mavenPom()
                artifact()
            }
        }
        maven {
            url 'https://jitpack.io'
        }
    }
}

subprojects {
    if (name == "main") {
        apply plugin: "com.android.application"
        apply plugin: 'com.google.gms.google-services'
    } else {
        apply plugin: "com.android.library"
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply plugin: 'kotlinx-serialization'
    apply plugin: 'io.objectbox'

    android {
        compileSdkVersion 30
        buildToolsVersion "30.0.3"

        if (name == "main") {
            defaultConfig {
                applicationId "com.poe.tradeapp"
                minSdkVersion 21
                targetSdkVersion 30
                versionCode 1
                versionName "1.0"
            }
        } else {
            defaultConfig {
                minSdkVersion 21
                targetSdkVersion 30
                versionCode 1
                versionName "1.0"
            }
        }

        dexOptions {
            preDexLibraries = true
            javaMaxHeapSize = "4g"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }

        buildFeatures {
            viewBinding true
        }
    }

    kapt {
        useBuildCache = true
        correctErrorTypes = true
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_version"
        implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:$kotlinx_serialization_version"

        implementation "org.koin:koin-core:$koin_version"
        implementation "org.koin:koin-android:$koin_version"
        implementation "org.koin:koin-core-ext:$koin_version"
        implementation "org.koin:koin-androidx-scope:$koin_version"
        implementation "org.koin:koin-androidx-viewmodel:$koin_version"
        implementation "org.koin:koin-androidx-fragment:$koin_version"
        implementation "org.koin:koin-androidx-workmanager:$koin_version"
        implementation "org.koin:koin-androidx-compose:$koin_version"
        implementation "org.koin:koin-androidx-ext:$koin_version"

        implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
        implementation "androidx.fragment:fragment-ktx:$fragmentktx_version"
        implementation "androidx.room:room-runtime:$room_version"
        implementation "androidx.room:room-ktx:$room_version"

        implementation "com.google.android.material:material:$material_library_version"
        implementation "com.google.android:flexbox:$flexbox_version"
        implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
        implementation "com.github.terrakok:cicerone:$cicerone_version"
        implementation "com.github.PhilJay:MPAndroidChart:$mp_android_chart_version"
        implementation "com.squareup.picasso:picasso:$picasso_version"
        implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"
        implementation "io.objectbox:objectbox-kotlin:$objectbox_version"

        implementation platform('com.google.firebase:firebase-bom:26.7.0')
        implementation 'com.google.firebase:firebase-messaging'
        implementation 'com.google.firebase:firebase-auth-ktx'
        implementation 'com.google.firebase:firebase-analytics-ktx'

        kapt "androidx.room:room-compiler:$room_version"

        if (name != "core") {
            implementation project(":core")
        }

        if (name == "main") {
            implementation project(":currency_feature")
            implementation project(":exchange_feature")
            implementation project(":charts_feature")
            implementation project(":notifications_feature")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}